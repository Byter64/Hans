.section Spieler

.set SpielerSchlaegerBreite, 2
.set SpielerSchlaegerHoehe, 10
.set SpielerGeschw, 1
.set SpielerFarbe, 0b11111110
SpielerPosX: .int 20
SpielerPosY: .int 60 - (SpielerSchlaegerHoehe / 2)


SpielerAktualisieren:
	Subi R31, R31, 3
	Store R31, R1, 1
	Store R31, R2, 2
	Store R31, R3, 3
	
	;Hole Knoepfe
	Load R1, R28, 0
	
	;Ist oben gedrueckt?
	Andi R2, R1, 0b100
	bez R2, SpielerUntenPruefen
	Load R2, R0, SpielerPosY
	Addi R3, R0, SpielRandOben + 1
	Cle R3, R2, R3
	bnez R3, SpielerUntenPruefen
	Subi R2, R2, SpielerGeschw
	Store R0, R2, SpielerPosY
	
	SpielerUntenPruefen:
	Andi R2, R1, 0b1000
	bez R2, SpielerLinksPruefen
	Load R2, R0, SpielerPosY
	Addi R3, R0, SpielRandUnten
	Subi R3, R3, SpielerSchlaegerHoehe + 1
	Cge R3, R2, R3
	bnez R3, SpielerLinksPruefen
	Addi R2, R2, SpielerGeschw
	Store R0, R2, SpielerPosY
	
	SpielerLinksPruefen:
	Andi R2, R1, 0b10000
	bez R2, SpielerRechtsPruefen
	Load R2, R0, SpielerPosX
	Addi R3, R0, SpielRandLinks + 1
	Cle R3, R2, R3
	bnez R3, SpielerRechtsPruefen
	Subi R2, R2, SpielerGeschw
	Store R0, R2, SpielerPosX
	
	SpielerRechtsPruefen:
	Andi R2, R1, 0b100000
	bez R2, SpielerAktualisierenEnde
	Load R2, R0, SpielerPosX
	Addi R3, R0, SpielRandRechts
	Subi R3, R3, SpielerSchlaegerBreite + 1
	Cg R3, R2, R3
	bnez R3, SpielerAktualisierenEnde
	Addi R2, R2, SpielerGeschw
	Store R0, R2, SpielerPosX
	
	SpielerAktualisierenEnde:
	Load R1, R31, 1
	Load R2, R31, 2
	Load R3, R31, 3
	Addi R31, R31, 3
	Jreg 30

SpielerZeichnen:
	Subi R31, R31, 6
	Store R31, R5, 1
	Store R31, R6, 2
	Store R31, R7, 3
	Store R31, R8, 4
	Store R31, R9, 5
	Store R31, R30, 6
	
	Load R5, R0, SpielerPosX
	Load R6, R0, SpielerPosY
	Addi R7, R0, SpielerSchlaegerBreite
	Addi R8, R0, SpielerSchlaegerHoehe
	Addi R9, R0, SpielerFarbe
	Jal R30, RechteckZeichnen
	
	Load R5, R31, 1
	Load R6, R31, 2
	Load R7, R31, 3
	Load R8, R31, 4
	Load R9, R31, 5
	Load R30, R31, 6
	Addi R31, R31, 6
Jreg R30
