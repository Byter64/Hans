.section Ball

BallPositionX: .int 60
BallPositionY: .int 60
BallGeschwX: .int 1
BallGeschwY: .int 1

.set BallGroesse, 3
.set BallFarbe, 255

BallAktualisieren:
	Subi R31, R31, 3
	Store R31, R1, 1
	Store R31, R2, 2
	Store R31, R3, 3
	
	Load R1, R0, BallPositionX
	Load R2, R0, BallGeschwX
	Add R1, R1, R2
	
	;Ist der Ball am rechten Rand?
	Addi R3, R0, SpielRandRechts
	Subi R3, R3, BallGroesse
	cge R3, R1, R3 ; X Koord == SpielRandRechts - BallGroesse
	bez R3, PruefeLinkenRand
	Addi R1, R0, SpielRandRechts
	Subi R1, R1, BallGroesse
	Sub R2, R0, R2
	Store R0, R2, BallGeschwX
	
	PruefeLinkenRand:
	;Ist der Ball am linken Rand?
	clei R3, R1, SpielRandLinks
	bez R3, SpeicherXKoordinate
	Addi R1, R0, SpielRandLinks
	Sub R2, R0, R2
	Store R0, R2, BallGeschwX
	
	SpeicherXKoordinate:
	Store R0, R1, BallPositionX
	
	Load R1, R0, BallPositionY
	Load R2, R0, BallGeschwY
	Add R1, R1, R2
	
	;Ist der Ball am unteren Rand?
	Addi R3, R0, SpielRandUnten
	Subi R3, R3, BallGroesse
	cge R3, R1, R3 ; Y Koord == SpielRandUnten - BallGroesse
	bez R3, PruefeOberenRand
	Addi R1, R0, SpielRandUnten
	Subi R1, R1, BallGroesse
	Sub R2, R0, R2
	Store R0, R2, BallGeschwY
	
	PruefeOberenRand:
	;Ist der Ball am oberen Rand?
	clei R3, R1, SpielRandOben
	bez R3, SpeicherYKoordinate
	Addi R1, R0, SpielRandOben
	Sub R2, R0, R2
	Store R0, R2, BallGeschwY
	
	SpeicherYKoordinate:
	Store R0, R1, BallPositionY
	
	
	BallAktualisierenEnde:
	Load R1, R31, 1
	Load R2, R31, 2
	Load R3, R31, 3
	Addi R31, R31, 3
Jreg R30;

BallKollisionenBearbeiten:
	Subi R31, R31, 3
	Store R31, R1, 1
	Store R31, R2, 2
	Store R31, R30, 3
	
	Jal R30, TesteSpielerBallKollision; Beschreibt R1
	bez R1, BallKollisionenBearbeitenEnde
	Load R1, R0, BallGeschwX
	cgei R2, R1, 0
	bnez R2, BallKollisionenBearbeitenEnde
	Sub R1, R0, R1
	Store R0, R1, BallGeschwX
	
	BallKollisionenBearbeitenEnde:
	Load R1, R31, 1
	Load R2, R31, 2
	Load R30, R31, 3
	Addi R31, R31, 3
Jreg R30

BallZeichnen:
	Subi R31, R31, 6
	Store R31, R5, 1
	Store R31, R6, 2
	Store R31, R7, 3
	Store R31, R8, 4
	Store R31, R9, 5
	Store R31, R30, 6
	
	Load R5, R0, BallPositionX
	Load R6, R0, BallPositionY
	Addi R7, R0, BallGroesse
	Addi R8, R0, BallGroesse
	Addi R9, R0, BallFarbe
	Jal R30, RechteckZeichnen
	
	Load R5, R31, 1
	Load R6, R31, 2
	Load R7, R31, 3
	Load R8, R31, 4
	Load R9, R31, 5
	Load R30, R31, 6
	Addi R31, R31, 6
Jreg R30